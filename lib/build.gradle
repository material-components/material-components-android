apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

archivesBaseName = getArchivesBaseName(project.name)
version = rootProject.ext.mdcLibraryVersion

dependencies {
  api compatibility("annotations")
  api compatibility("compat")
  api compatibility("core_ui")
  api compatibility("core_utils")
  api compatibility("fragment")
  api compatibility("transition")
  api compatibility("v7/appcompat")
  api compatibility("v7/cardview")
  api compatibility("v7/recyclerview")

  testImplementation("com.android.support.test:runner:${project.rootProject.ext.testRunnerVersion}") {
    exclude module: 'support-annotations'
  }
  testImplementation "junit:junit:4.12"
  testImplementation "com.google.truth:truth:0.35"
  testImplementation "org.mockito:mockito-core:1.9.5"
  testImplementation "org.robolectric:robolectric:3.3.2"
}

def srcDirs = [
  'android/support/design/animation',
  'android/support/design/bottomappbar',
  'android/support/design/bottomnavigation',
  'android/support/design/bottomsheet',
  'android/support/design/button',
  'android/support/design/canvas',
  'android/support/design/card',
  'android/support/design/chip',
  'android/support/design/circularreveal',
  'android/support/design/circularreveal/cardview',
  'android/support/design/circularreveal/coordinatorlayout',
  'android/support/design/color',
  'android/support/design/dialog',
  'android/support/design/drawable',
  'android/support/design/expandable',
  'android/support/design/floatingactionbutton',
  'android/support/design/internal',
  'android/support/design/math',
  'android/support/design/navigation',
  'android/support/design/resources',
  'android/support/design/ripple',
  'android/support/design/shape',
  'android/support/design/snackbar',
  'android/support/design/stateful',
  'android/support/design/tabs',
  'android/support/design/textfield',
  'android/support/design/theme',
  'android/support/design/transformation',
  'android/support/design/typography',
  'android/support/design/widget',
]

android {
  sourceSets {
    main.manifest.srcFile 'java/android/support/design/AndroidManifest.xml'
    main.java.srcDir 'java'
    main.java.includes = srcDirs.collect { it + "/**/*.java" }
    main.java.excludes = [
      '**/build/**',
    ]
    srcDirs.forEach {
      main.res.srcDirs += "java/" + it + "/res"
      main.res.srcDirs += "java/" + it + "/res-public"
    }

    test.java.srcDir 'javatests'
  }

  buildTypes.all {
    consumerProguardFiles 'proguard-rules.pro'
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: rootProject.ext.mavenRepoUrl)
    }
  }
}
