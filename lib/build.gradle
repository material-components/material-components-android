apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

archivesBaseName = getArchivesBaseName(project.name)
version = rootProject.ext.mdcLibraryVersion

dependencies {
  api compatibility("annotations")
  api compatibility("compat")
  api compatibility("core_ui")
  api compatibility("core_utils")
  api compatibility("fragment")
  api compatibility("transition")
  api compatibility("v7/appcompat")
  api compatibility("v7/cardview")
  api compatibility("v7/recyclerview")

  testImplementation("com.android.support.test:runner:${project.rootProject.ext.testRunnerVersion}") {
    exclude module: 'support-annotations'
  }
  testImplementation "junit:junit:4.12"
  testImplementation "com.google.truth:truth:0.35"
  testImplementation "org.mockito:mockito-core:1.9.5"
  testImplementation "org.robolectric:robolectric:3.3.2"
}

def srcDirs = [
  'com/google/android/material/animation',
  'com/google/android/material/appbar',
  'com/google/android/material/behavior',
  'com/google/android/material/bottomappbar',
  'com/google/android/material/bottomnavigation',
  'com/google/android/material/bottomsheet',
  'com/google/android/material/button',
  'com/google/android/material/canvas',
  'com/google/android/material/card',
  'com/google/android/material/chip',
  'com/google/android/material/circularreveal',
  'com/google/android/material/circularreveal/cardview',
  'com/google/android/material/circularreveal/coordinatorlayout',
  'com/google/android/material/color',
  'com/google/android/material/dialog',
  'com/google/android/material/drawable',
  'com/google/android/material/expandable',
  'com/google/android/material/floatingactionbutton',
  'com/google/android/material/internal',
  'com/google/android/material/math',
  'com/google/android/material/navigation',
  'com/google/android/material/resources',
  'com/google/android/material/ripple',
  'com/google/android/material/shape',
  'com/google/android/material/shadow',
  'com/google/android/material/snackbar',
  'com/google/android/material/stateful',
  'com/google/android/material/tabs',
  'com/google/android/material/textfield',
  'com/google/android/material/theme',
  'com/google/android/material/transformation',
  'com/google/android/material/typography',
]

android {
  sourceSets {
    main.manifest.srcFile 'java/com/google/android/material/AndroidManifest.xml'
    main.java.srcDir 'java'
    main.java.includes = srcDirs.collect { it + '/**/*.java' }
    main.java.excludes = [
      '**/build/**',
    ]
    srcDirs.forEach {
      main.res.srcDirs += 'java/' + it + '/res'
      main.res.srcDirs += 'java/' + it + '/res-public'
    }

    test.java.srcDir 'javatests'
  }

  buildTypes.all {
    consumerProguardFiles 'proguard-behaviors.pro'
  }
}

// TODO: setup API txt generation

task generateJavadocs(type: Javadoc) {
  if (project.hasProperty("online")) {
    options.addStringOption("toroot", "/")
    options.addStringOption("hdf", "android.whichdoc online")
    options.addStringOption("hdf", "dac")
    options.addBooleanOption("devsite", true)
    options.addBooleanOption("yamlV2", true)
    options.addStringOption("dac_libraryroot", "com/google/android/material")
    options.addStringOption("dac_dataname", "MATERIAL_DATA")
  }

  if (project.hasProperty("docletPathRoot")) {
    def docletPathRoot = project.property("docletPathRoot")

    source = android.sourceSets.main.java.source
    source = source.findAll { it.name.endsWith(".java") }
    title = null
    destinationDir = new File("doclava-out")
    classpath = files("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
    options.addStringOption("federate Android", "https://developer.android.com")
    options.encoding = "UTF-8"
    options.doclet = "com.google.doclava.Doclava"
    options.docletpath = [
      file(docletPathRoot + "/doclava/current/doclava.jar"),
      file(docletPathRoot + "/jsilver/v1_0_0/jsilver.jar")
    ]
    // TODO: remove once all errors are resolved
    failOnError false
  }
}

afterEvaluate {
  generateJavadocs.classpath += files(android.libraryVariants.collect { variant ->
    variant.javaCompile.classpath.files
  })
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: rootProject.ext.mavenRepoUrl)
    }
  }
}
