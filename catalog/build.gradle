apply plugin: 'com.android.application'

dependencies {
  api 'com.google.dagger:dagger:2.23.1'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.23.1'

  api 'com.google.dagger:dagger-android:2.23.1'
  api 'com.google.dagger:dagger-android-support:2.23.1'
  annotationProcessor 'com.google.dagger:dagger-android-processor:2.23.1'

  api 'androidx.multidex:multidex:2.0.0'

  api 'com.google.guava:guava:27.0.1-android'

  api project(':lib')

  api compatibility("recyclerViewSelection")

  androidTestImplementation "androidx.test:core:${project.rootProject.ext.testRunnerVersion}"
  androidTestImplementation "androidx.test:runner:${project.rootProject.ext.testRunnerVersion}"
  androidTestImplementation "androidx.test:rules:${project.rootProject.ext.testRunnerVersion}"
  androidTestImplementation "androidx.test.espresso:espresso-core:${project.rootProject.ext.espressoVersion}"
  androidTestImplementation "androidx.test.espresso:espresso-contrib:${project.rootProject.ext.espressoVersion}"

  testAnnotationProcessor "com.google.auto.service:auto-service:1.0-rc4"
}

def srcDirs = [
  'application',
  'application/attrs',
  'application/legacymultidex',
  'application/scope',
  'application/theme',
  'assets',
  'bottomappbar',
  'bottomnav',
  'bottomsheet',
  'button',
  'card',
  'checkbox',
  'chip',
  'datepicker',
  'dialog',
  'draggable',
  'elevation',
  'fab',
  'feature',
  'font',
  'internal',
  'main',
  'menu',
  'radiobutton',
  'shapetheming',
  'switchmaterial',
  'tableofcontents',
  'tabs',
  'textfield',
  'themeswitcher',
  'topappbar',
  'transformation',
  'windowpreferences'
]

android {
  defaultConfig {
    manifestPlaceholders = [
      application_name             : 'CatalogApplication',
      application_theme            : 'Catalog',
      catalog_application_component: 'io.material.catalog.application.DaggerCatalogApplicationComponent'
    ]

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true
    minSdkVersion 14
    targetSdkVersion 27
  }

  buildTypes {
    release {
      multiDexKeepProguard file('multidex-config.pro')
    }
  }


  testOptions {
    unitTests.returnDefaultValues = true
  }

  aaptOptions {
    additionalParameters "--no-version-vectors"
  }

  sourceSets {
    main {
      manifest.srcFile 'java/io/material/catalog/AndroidManifest.xml'
      java.srcDir 'java'
      java.includes = srcDirs.collect { 'io/material/catalog/' + it + '/**/*.java' }
      java.excludes = [
        '**/build/**',
      ]
      res.srcDirs = ['java/io/material/catalog/res']
      srcDirs.forEach {
        res.srcDirs += 'java/io/material/catalog/' + it + '/res'
      }
    }
    androidTest.java.srcDir 'javatests'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}
