apply plugin: 'com.android.application'

dependencies {
  api 'com.google.dagger:dagger:2.28.1'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.28.1'

  api 'com.google.dagger:dagger-android:2.28.1'
  api 'com.google.dagger:dagger-android-support:2.28.1'
  annotationProcessor 'com.google.dagger:dagger-android-processor:2.28.1'

  api 'androidx.multidex:multidex:2.0.1'
  api 'androidx.constraintlayout:constraintlayout:2.1.0'
  api 'androidx.gridlayout:gridlayout:1.0.0'
  api "androidx.multidex:multidex:2.0.1"
  api "androidx.recyclerview:recyclerview:1.2.1"
  api 'androidx.window:window:1.0.0-beta02'
  api "androidx.window:window-java:1.0.0-beta02"

  api 'com.google.guava:guava:27.0.1-android'

  api project(':lib')

  api compatibility("recyclerViewSelection")

  androidTestImplementation "androidx.test:core:${project.rootProject.ext.testRunnerVersion}"
  androidTestImplementation "androidx.test:runner:${project.rootProject.ext.testRunnerVersion}"
  androidTestImplementation "androidx.test:rules:${project.rootProject.ext.testRunnerVersion}"
  androidTestImplementation "androidx.test.espresso:espresso-core:${project.rootProject.ext.espressoVersion}"
  androidTestImplementation "androidx.test.espresso:espresso-contrib:${project.rootProject.ext.espressoVersion}"

  testImplementation "androidx.test:core:${project.rootProject.ext.testRunnerVersion}"
  testImplementation "androidx.test:runner:${project.rootProject.ext.testRunnerVersion}"
  testImplementation "junit:junit:4.12"
  testImplementation "com.google.truth:truth:${project.rootProject.ext.truthVersion}"
  testImplementation "org.mockito:mockito-core:${project.rootProject.ext.mockitoCoreVersion}"
  testImplementation "org.robolectric:robolectric:${project.rootProject.ext.robolectricVersion}"

  testAnnotationProcessor "com.google.auto.service:auto-service:1.0-rc4"
}

def srcDirs = [
  'adaptive',
  'application',
  'application/attrs',
  'application/legacymultidex',
  'application/scope',
  'application/theme',
  'assets',
  'bottomappbar',
  'bottomnav',
  'bottomsheet',
  'button',
  'card',
  'checkbox',
  'chip',
  'color',
  'datepicker',
  'dialog',
  'divider',
  'draggable',
  'elevation',
  'imageview',
  'fab',
  'feature',
  'font',
  'internal',
  'main',
  'menu',
  'musicplayer',
  'navigationrail',
  'preferences',
  'progressindicator',
  'radiobutton',
  'shapetheming',
  'slider',
  'switchmaterial',
  'tableofcontents',
  'tabs',
  'textfield',
  'themeswitcher',
  'timepicker',
  'topappbar',
  'transition',
  'windowpreferences'
]

android {
  defaultConfig {
    manifestPlaceholders = [
      application_name             : 'CatalogApplication',
      application_theme            : 'Catalog',
      catalog_application_component: 'io.material.catalog.application.DaggerCatalogApplicationComponent'
    ]

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true
    multiDexEnabled true
    minSdkVersion 14
    targetSdkVersion 31
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  aaptOptions {
    additionalParameters "--no-version-vectors"
  }

  sourceSets {
    main {
      manifest.srcFile 'java/io/material/catalog/AndroidManifest.xml'
      java.srcDir 'java'
      java.includes = srcDirs.collect {
        'io/material/catalog/' + it + '/**/*.java'
      }
      java.excludes = [
        '**/build/**',
      ]
      res.srcDirs = ['java/io/material/catalog/res']
      srcDirs.forEach {
        res.srcDirs += 'java/io/material/catalog/' + it + '/res'
      }
    }
    //TODO(b/141695010)
    // androidTest.java.srcDir 'androidTest/javatests'
    // test.java.srcDir 'test/javatests'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}
